plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
}

kotlin {
    jvmToolchain 17
}

android {
    namespace 'com.example.study.accountmanager.authenticator'

    defaultConfig {
        applicationId 'com.example.study.accountmanager.authenticator'
        versionCode 1
        versionName '0.1.0'
        signingConfig signingConfigs.debug
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro'
        }
        benchmark {
            initWith(release)
            proguardFiles 'benchmark-rules.pro'
        }
    }

    composeOptions {
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin#pre-release_kotlin_compatibility
        kotlinCompilerExtensionVersion '1.5.3'
    }

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/*.kotlin_module']
        }
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.p12")
        }
    }
}

dependencies {
    implementation projects.common
    implementation projects.contract
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.compose.uiTooling
    implementation libs.androidx.compose.material.material
    implementation libs.androidx.lifecycle.service
    implementation libs.androidx.profileInstaller
}
