plugins {
    alias libs.plugins.android.test
    alias libs.plugins.kotlin.android
}

kotlin {
    jvmToolchain 17
}

android {
    namespace 'com.example.study.accountmanager.macrobenchmark'

    targetProjectPath = System.getProperty("targetProject") ?: ':app'
    experimentalProperties['android.experimental.self-instrumenting'] = true

    defaultConfig {
        signingConfig signingConfigs.debug
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        benchmark {
            initWith(debug)
            matchingFallbacks += 'release'
        }
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.p12")
        }
    }
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == 'benchmark'
    }
}

dependencies {
    implementation(libs.androidx.benchmark)
    implementation(libs.androidx.test.junit)
}
